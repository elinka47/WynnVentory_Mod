plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven { url = 'https://maven.neoforged.net/releases' }
}

dependencies {
    neoForge "net.neoforged:neoforge:$neoforge_loader_version"
    modRuntimeOnly "me.djtheredstoner:DevAuth-neoforge:${devauth_version}"

    modImplementation("maven.modrinth:wynntils:v${project.wynntils_version}-neoforge")

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    runtimeOnly("com.fasterxml.jackson.core:jackson-databind:${jackson_version}")
    runtimeOnly("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jackson_version}")
    runtimeOnly("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jackson_version}")
    shadowBundle("com.fasterxml.jackson.core:jackson-databind:${jackson_version}")
    shadowBundle("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jackson_version}")
    shadowBundle("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jackson_version}")
}

processResources {
    var replaceProperties = [
        mod_version                : rootProject.version,
        minecraft_version          : minecraft_version,
        neoforge_loader_version    : neoforge_loader_version,
        wynntils_version           : wynntils_version
    ]
    inputs.properties replaceProperties

    filesMatching([
        'META-INF/neoforge.mods.toml'
    ]) {
        expand replaceProperties
    }
}

shadowJar {
    configurations = [
        project.configurations.shadowBundle
    ]

    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
