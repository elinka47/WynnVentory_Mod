plugins {
    id 'dev.architectury.loom' version "${architectury_loom_version}" apply false
    id 'architectury-plugin' version "${architectury_plugin_version}"
    id 'com.gradleup.shadow' version '8.3.6' apply false
    id "com.diffplug.spotless" version "${spotless_version}"
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version

    apply plugin: "java"
    apply plugin: "com.diffplug.spotless"

    spotless {
        java {
            // define the steps to apply to Java source code
            importOrder()
            removeUnusedImports()
            palantirJavaFormat(spotless_palantir_version)
            trimTrailingWhitespace()
            endWithNewline()
            ratchetFrom('origin/master')

            custom "Refuse wildcard imports", {
                if (it =~ /\nimport .*\*;/) {
                    throw new RuntimeException("Do not use wildcard imports. 'spotlessApply' cannot resolve this issue.")
                }
                return it
            }
            custom "Refuse IntelliJ annotations", {
                if (it =~ /\nimport org\.jetbrains\.annotations\./) {
                    throw new RuntimeException("Do not use IntelliJ annotations. 'spotlessApply' cannot resolve this issue.")
                }
                return it
            }
            custom "No empty line after opening curly brace", {
                it.replaceAll(/\{\n\n/, '{\n')
            }
        }
        json {
            target "**/src/**/*.json"
            /*
             1. spotless replaces the Unicode escapes with the actual characters, worsening readability.
             2. schemaVersion must be at the top or else Fabric will complain
             */
            targetExclude(
                    "**/src/main/resources/assets/wynntils/font/*.json",
                    "**/src/main/resources/fabric.mod.json")
            gson()
                    .indentWithSpaces(2)
                    .sortByKeys()
            trimTrailingWhitespace()
            endWithNewline()
        }
        format "lang", {
            target "**/src/main/resources/assets/wynntils/lang/*.json"
            custom "No empty language json files", {
                return it.replaceAll(/^\{\}\n$/, '')
            }
        }
        groovyGradle {
            target '**/*.gradle'
            greclipse("${spotless_greclipse_version}")
            leadingTabsToSpaces(4)
            trimTrailingWhitespace()
            endWithNewline()
        }

        format "misc", {
            // define the files to apply `misc` to
            target "*.gradle", "*.md", ".gitignore", "*.properties"
            targetExclude("CHANGELOG.md")

            // define the steps to apply to those files
            trimTrailingWhitespace()
            leadingTabsToSpaces()
            endWithNewline()
        }
    }
}

tasks.register('installGitHooks', Copy) {
    from new File(rootProject.rootDir, 'gradle/git-hooks')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0775
}

tasks.named('build') {
    dependsOn 'installGitHooks'
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        maven { url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1" }
        maven { url = "https://api.modrinth.com/maven" }
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }
}
