plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
    implementation.extendsFrom shadowImplementation

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        url "https://maven.terraformersmc.com/"
    }
}

dependencies {
    modImplementation("net.fabricmc:fabric-loader:$fabric_loader_version")
    modImplementation("maven.modrinth:wynntils:v${project.wynntils_version}-fabric")

    modRuntimeOnly(fabricApi.module("fabric-api-base", fabric_api_version))
    modRuntimeOnly(fabricApi.module("fabric-resource-loader-v1", fabric_api_version))
    modRuntimeOnly("me.djtheredstoner:DevAuth-fabric:${devauth_version}")

    modCompileOnly("com.terraformersmc:modmenu:${modmenu_version}")

    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }

    shadowImplementation("net.neoforged:bus:${neoforge_eventbus_version}") {
        exclude group: "org.ow2.asm"
        exclude group: "org.apache.logging.log4j"
        exclude group: "cpw.mods", module: "modlauncher"
    }
    shadowImplementation("com.fasterxml.jackson.core:jackson-databind:${jackson_version}")
    shadowImplementation("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jackson_version}")
    shadowImplementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jackson_version}")

    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    var replaceProperties = [
        mod_version                : rootProject.version,
        minecraft_version          : minecraft_version,
        fabric_loader_version      : fabric_loader_version,
        wynntils_version           : wynntils_version,
        modmenu_version            : modmenu_version
    ]
    inputs.properties replaceProperties

    filesMatching("fabric.mod.json") {
        expand replaceProperties
    }
}

shadowJar {
    configurations = [
        project.configurations.shadowCommon,
        project.configurations.shadowImplementation
    ]

    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
